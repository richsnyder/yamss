CMAKE_MINIMUM_REQUIRED(VERSION 3.0.0)

PROJECT(yamss LANGUAGES C CXX VERSION 3.2.0)

# Add custom modules to the module search path and mark them for installation.

LIST(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake/Modules)

# Include CMake files.

INCLUDE(Documentation)
INCLUDE(Reporting)

# Locate all of the required dependencies.

FIND_PACKAGE(Armadillo REQUIRED)
INCLUDE_DIRECTORIES(${Armadillo_INCLUDE_DIRS})
LIST(APPEND EXTRA_LIBS ${Armadillo_LIBRARIES})

SET(BOOST_COMPONENTS filesystem program_options regex system)
FIND_PACKAGE(Boost 1.55.0 COMPONENTS ${BOOST_COMPONENTS} REQUIRED)
INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIRS})
LIST(APPEND EXTRA_LIBS ${Boost_LIBRARIES})

FIND_PACKAGE(Lua REQUIRED)
INCLUDE_DIRECTORIES(${LUA_INCLUDE_DIR})
LIST(APPEND EXTRA_LIBS ${LUA_LIBRARIES})

# Check for the optional dependencies.

SET(BUILD_SERVER "AUTO" CACHE STRING "Build support for server mode")
SET_PROPERTY(CACHE BUILD_SERVER PROPERTY STRINGS AUTO ON OFF)
IF(BUILD_SERVER STREQUAL ON)
  FIND_PACKAGE(CURL REQUIRED)
  FIND_PACKAGE(Hermes REQUIRED)
ELSEIF(BUILD_SERVER STREQUAL AUTO)
  FIND_PACKAGE(CURL)
  FIND_PACKAGE(Hermes)
  IF(CURL_FOUND AND Hermes_FOUND)
    SET(BUILD_SERVER ON)
  ELSE()
    SET(BUILD_SERVER OFF)
  ENDIF()
ENDIF()
IF(BUILD_SERVER)
  INCLUDE_DIRECTORIES(${CURL_INCLUDE_DIRS} ${Hermes_INCLUDE_DIRS})
  LIST(APPEND EXTRA_LIBS ${CURL_LIBRARIES} ${Hermes_LIBRARIES})
  SET(YAMSS_SUPPORTS_SERVER_MODE ON)
ENDIF()

SET(BUILD_WRAPPER "AUTO" CACHE STRING "Build a Java wrapper")
SET_PROPERTY(CACHE BUILD_WRAPPER PROPERTY STRINGS AUTO ON OFF)
IF(BUILD_WRAPPER STREQUAL ON)
  FIND_PACKAGE(SWIG REQUIRED)
  FIND_PACKAGE(Java REQUIRED)
  FIND_PACKAGE(JNI REQUIRED)
ELSEIF(BUILD_WRAPPER STREQUAL AUTO)
  FIND_PACKAGE(SWIG)
  FIND_PACKAGE(Java)
  FIND_PACKAGE(JNI)
  IF(SWIG_FOUND AND Java_FOUND AND JNI_FOUND)
    SET(BUILD_WRAPPER ON)
  ELSE()
    SET(BUILD_WRAPPER OFF)
  ENDIF()
ENDIF()
IF(BUILD_WRAPPER)
  INCLUDE_DIRECTORIES(${JNI_INCLUDE_DIRS})
  LIST(APPEND EXTRA_LIBS ${JNI_LIBRARIES})
ENDIF()

# Report on the build configuration.

CONFIGURATION_REPORT()

# Add the project subdirectories.

ADD_SUBDIRECTORY(src)
ADD_SUBDIRECTORY(doc)
ADD_SUBDIRECTORY(examples)
